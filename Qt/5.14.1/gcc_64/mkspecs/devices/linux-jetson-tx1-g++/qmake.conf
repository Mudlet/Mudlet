#
# qmake configuration for Jetson TX1 boards running 64-bit Linux For Tegra
# (tested with R24.2, sample root filesystem)
#
# Note that this environment has been tested with X11 only.
#
# A typical configure line might look like the following:
#
# configure \
#   -device linux-jetson-tx1-g++ \
#   -device-option CROSS_COMPILE=/opt/gcc-linaro-5.3.1-2016.05-x86_64_aarch64-linux-gnu/bin/aarch64-linux-gnu- \
#   -sysroot /opt/Linux_for_Tegra/rootfs
#
# Note that this builds for GLX + OpenGL. To use EGL + OpenGL ES instead, pass
# -opengl es2 and ensure the rootfs has the headers (rootfs/usr/include/EGL,
# GLES2, GLES3), which may not be the case out of the box.
#
# Check the configure output carefully, some features may be disabled due to the
# rootfs not having the necessary dev files.
#
# If getting cryptic linker errors from static libs like libm.a, check that the
# symlinks libm.so, libz.so, etc. under rootfs/usr/lib/aarch64-linux-gnu are not
# broken. If they are, due to using absolute paths, change them so that they are
# relative to rootfs.

include(../common/linux_device_pre.conf)

QMAKE_INCDIR_POST += \
    $$[QT_SYSROOT]/usr/include \
    $$[QT_SYSROOT]/usr/include/aarch64-linux-gnu

QMAKE_LIBDIR_POST += \
    $$[QT_SYSROOT]/usr/lib \
    $$[QT_SYSROOT]/lib/aarch64-linux-gnu \
    $$[QT_SYSROOT]/usr/lib/aarch64-linux-gnu

QMAKE_RPATHLINKDIR_POST += \
    $$[QT_SYSROOT]/usr/lib \
    $$[QT_SYSROOT]/usr/lib/aarch64-linux-gnu \
    $$[QT_SYSROOT]/usr/lib/aarch64-linux-gnu/tegra \
    $$[QT_SYSROOT]/lib/aarch64-linux-gnu

DISTRO_OPTS += aarch64
COMPILER_FLAGS += -mtune=cortex-a57.cortex-a53 -march=armv8-a

# When configured with -opengl es2, eglfs will be functional with its
# dummy fullscreen X11 backend, in addition to xcb.
EGLFS_DEVICE_INTEGRATION = eglfs_x11

include(../common/linux_arm_device_post.conf)
load(qt_config)
