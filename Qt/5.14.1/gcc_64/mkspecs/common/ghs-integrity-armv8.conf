#
# Base qmake configuration for INTEGRITY armv8 targets
#
MAKEFILE_GENERATOR      = UNIX

QMAKE_PLATFORM          = integrity

include(unix.conf)

include(ghs-base.conf)

bsp_name = $$(INTEGRITY_BSP)
isEmpty(bsp_name): \
    error("This qmakespec requires $INTEGRITY_BSP to be set")

os_directory = $$(INTEGRITY_DIR)
isEmpty(os_directory): \
    error("This qmakespec requires $INTEGRITY_DIR to be set")

QMAKE_CC                = cxintarm64 -bsp $$bsp_name -os_dir $$os_directory -non_shared
QMAKE_CXX               = cxintarm64 -bsp $$bsp_name -os_dir $$os_directory -non_shared
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_AR                = $$QMAKE_CXX -archive -o

QMAKE_CFLAGS            += -bigswitch
QMAKE_CXXFLAGS          += -bigswitch
QMAKE_LFLAGS            += -bigswitch
