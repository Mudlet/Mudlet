#
# qmake configuration for openbsd-g++
#

MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = openbsd

include(../common/bsd/bsd.conf)

# Addon software goes into /usr/local on OpenBSD, by default we will look there
QMAKE_INCDIR_POST       = /usr/local/include
QMAKE_LIBDIR_POST       = /usr/local/lib

# System provided X11 defaults to X11R6 path on OpenBSD
QMAKE_INCDIR_X11        = /usr/X11R6/include
QMAKE_LIBDIR_X11        = /usr/X11R6/lib
QMAKE_INCDIR_OPENGL     = /usr/X11R6/include
QMAKE_LIBDIR_OPENGL     = /usr/X11R6/lib

QMAKE_RPATHDIR         += $$QMAKE_LIBDIR_X11

include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

# System compiler is gcc 4.2.1 up to OpenBSD 6.0.
# For proper C++11 support, we need to use a newer gcc from ports/packages,
# where compiler commands are renamed to egcc/eg++. Therefore, redefine
# mkspecs/common/g++-base.conf compiler commands
QMAKE_CC                = egcc

QMAKE_LINK_C            = $$QMAKE_CC
QMAKE_LINK_C_SHLIB      = $$QMAKE_CC

QMAKE_CXX               = eg++

QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX


# Reset g++-unix.conf's NOUNDEF flags as OpenBSD libc can't handle environ
QMAKE_LFLAGS_NOUNDEF    =

load(qt_config)
