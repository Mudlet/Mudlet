######################################################################
# Communi
######################################################################

isEmpty(COMMUNI):error(Usage: \"CONFIG += communi\" and \"COMMUNI += <module(s)>\" \(available modules: core model util\))

include(communi-config.prf)

isEmpty(IRC_INSTALL_LIBS):IRC_INSTALL_LIBS = $$[QT_INSTALL_LIBS]
isEmpty(IRC_INSTALL_BINS):IRC_INSTALL_BINS = $$[QT_INSTALL_BINS]
isEmpty(IRC_INSTALL_HEADERS):IRC_INSTALL_HEADERS = $$[QT_INSTALL_HEADERS]/Communi

macx:!qt_no_framework {
    QMAKE_FRAMEWORKPATH += $$IRC_INSTALL_LIBS
} else {
    INCLUDEPATH += $$IRC_INSTALL_HEADERS
    DEPENDPATH += $$IRC_INSTALL_HEADERS
    LIBS += -L$$IRC_INSTALL_LIBS
}

IRC_MODULES =
for(MODULE, COMMUNI) {
    MODULE = $$lower($$MODULE)
    isEqual(MODULE, "core"):IRC_MODULES *= IrcCore
    else:isEqual(MODULE, "model"):IRC_MODULES *= IrcModel
    else:isEqual(MODULE, "util"):IRC_MODULES *= IrcUtil
}

# order matters for static libs
IRC_MODULES_ORDERED =
for(IRC_MODULE, $$list(IrcUtil IrcModel IrcCore)) {
   contains(IRC_MODULES, $$IRC_MODULE):IRC_MODULES_ORDERED += $$IRC_MODULE
}

for(IRC_MODULE, IRC_MODULES_ORDERED) {
    macx:!qt_no_framework {
        INCLUDEPATH += $$IRC_INSTALL_LIBS/$${IRC_MODULE}.framework/Headers
        LIBS += -framework $$IRC_MODULE
    } else {
        INCLUDEPATH += $$IRC_INSTALL_HEADERS/$$IRC_MODULE
        DEPENDPATH += $$IRC_INSTALL_HEADERS/$$IRC_MODULE
        REAL_TEMPLATE = $$TEMPLATE
        TEMPLATE = fakelib
        LIBS += -l$$qtLibraryTarget($$IRC_MODULE)
        TEMPLATE = $$REAL_TEMPLATE
    }
}
